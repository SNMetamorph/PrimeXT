import java.time.LocalDateTime
import java.time.Month
import java.time.temporal.ChronoUnit

apply plugin: 'com.android.application'

android {
    ndkVersion '26.1.10909125'
    namespace 'su.xash.primext'

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    defaultConfig {
        applicationId 'su.xash'
        applicationIdSuffix 'primext'
        versionName '1.0'
        versionCode getBuildNum()
        minSdkVersion 19
        targetSdk 34
        compileSdk 34

        ndk {
            abiFilters 'arm64-v8a', 'x86_64', 'armeabi-v7a'
        }

        externalNativeBuild {
            cmake {
                arguments '-DVCPKG_TARGET_ANDROID=ON', '-DBUILD_UTILS=OFF', '-DBUILD_GAME_LAUNCHER=OFF'
            }
        }
    }

    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('../../CMakeLists.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lint {
        abortOnError false
    }

    sourceSets {
        main {
            assets {
                srcDirs += "${project.projectDir}/../../game_dir"
            }
        }
    }

    androidResources {
        noCompress += ''
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
}

static def getBuildNum() {
    LocalDateTime now = LocalDateTime.now()
    LocalDateTime releaseDate = LocalDateTime.of(2023, Month.OCTOBER, 30, 0, 0, 0)
    int qBuildNum = releaseDate.until(now, ChronoUnit.DAYS)
    int minuteOfDay = now.getHour() * 60 + now.getMinute()
    return qBuildNum * 10000 + minuteOfDay
}